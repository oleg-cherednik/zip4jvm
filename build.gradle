buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id 'io.codearte.nexus-staging' version '0.30.0'
//    id "com.github.hierynomus.license" version "0.15.0"
}

apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenLocal()
    mavenCentral()
}

group 'ru.oleg-cherednik.zip4jvm'
version '1.7'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
mainClassName = 'unknown'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    jar.archiveName = "zip4jvm-${version}.jar"
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'

    annotationProcessor('org.projectlombok:lombok:1.18.20')

    testImplementation 'org.testng:testng:7.4.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'org.apache.commons:commons-compress:1.21'
    testImplementation 'net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01'
    testImplementation 'net.sf.sevenzipjbinding:sevenzipjbinding-all-platforms:16.02-2.01'
    testImplementation 'net.lingala.zip4j:zip4j:2.7.0'
    testImplementation 'de.idyl:winzipaes:1.0.1'
    testImplementation 'org.tukaani:xz:1.9'
    testImplementation 'com.github.luben:zstd-jni:1.5.0-4'

    testAnnotationProcessor('org.projectlombok:lombok:1.18.20')
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

test {
    useTestNG()
}

//license {
//    header = file('license_header.txt')
//    include "**/*.java"
//    exclude "**/*Test.java"
//}

modifyPom {
    project {
        name 'zip4jvm'
        description 'Zip files support for JDK application'
        url 'https://github.com/oleg-cherednik/zip4jvm'
        inceptionYear '2019'

        scm {
            url 'https://github.com/oleg-cherednik/zip4jvm'
            connection 'scm:https://github.com/oleg-cherednik/zip4jvm.git'
            developerConnection 'scm:git@github.com:oleg-cherednik/zip4jvm.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'oleg.cherednik'
                name 'Oleg Cherednik'
                email 'oleg.cherednik@gmail.com'
            }
        }
    }
}

extraArchive {
    sources true
    tests true
    javadoc true
}

nexus {
    sign true
    repositoryUrl 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
    snapshotRepositoryUrl 'https://oss.sonatype.org/content/repositories/snapshots'
}

nexusStaging {
    packageGroup 'ru.oleg-cherednik'
    stagingProfileId 'd3835bf60f05b9'
}
