buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'jacoco'
    id 'java-library'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'

    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group 'ru.oleg-cherednik.zip4jvm'
version '1.8'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    jar.archiveName = "${rootProject.name}-${version}.jar"
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-codec:commons-codec:1.15'

    implementation 'com.github.luben:zstd-jni:1.5.0-4'

    annotationProcessor('org.projectlombok:lombok:1.18.20')

    testImplementation 'org.testng:testng:7.4.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation 'org.apache.commons:commons-compress:1.22'
    testImplementation 'net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01'
    testImplementation 'net.sf.sevenzipjbinding:sevenzipjbinding-all-platforms:16.02-2.01'
    testImplementation 'net.lingala.zip4j:zip4j:2.7.0'
    testImplementation 'de.idyl:winzipaes:1.0.1'
    testImplementation 'org.tukaani:xz:1.9'

    testAnnotationProcessor('org.projectlombok:lombok:1.18.20')
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestReport

test {
    useTestNG()
}

license {
    header rootProject.file('APACHE.txt')
    include '**/*.java'
    newLine false
}

signing {
    sign publishing.publications
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = rootProject.name

                description = 'Zip files support for JDK application'
                url = 'https://github.com/oleg-cherednik/zip4jvm'
                inceptionYear = '2019'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'oleg.cherednik'
                        name = 'Oleg Cherednik'
                        email = 'oleg.cherednik@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/oleg-cherednik/zip4jvm'
                    connection = 'scm:https://github.com/oleg-cherednik/zip4jvm.git'
                    developerConnection = 'scm:git@github.com:oleg-cherednik/zip4jvm.git'
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}
